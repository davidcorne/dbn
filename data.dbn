Command circle x y radius
{
   // The circle equation here is (i - x)^2 + (j - y)^2 = r^2
   // For each pixel [i, j], check to see if (i - x)^2 + (j - y)^2 < r^2
   Set r2 (radius * radius)
   Set x_min (x - radius)
   Smaller? x_min 0 { Set x_min 0}
   Set x_max (x + radius)
   NotSmaller? x_max 100 { Set x_max 100}
   Set y_min (y - radius)
   Smaller? y_min 0 { Set y_min 0}
   Set y_max (y + radius)
   NotSmaller? y_max 100 { Set y_max 100}
   Repeat i x_min x_max
   {
       Repeat j y_min y_max
       {
           Set distance (((i - x) * (i - x)) + ((j - y) * (j - y)))
           Smaller? distance r2
           {
               Set [i j] 100
           }
       }
   }
}
Paper 0
Pen 100
circle 50 50 15

